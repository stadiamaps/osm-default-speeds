start: _speed_defs

_speed_defs: _speed_def
           | _speed_def "," _speed_defs

_speed_def: single_speed_def
          | multilane_speed

single_speed_def: _speed             -> normal_speed
                | "advisory:" _speed -> advisory_speed

multilane_speed: single_speed_def ("|" single_speed_def)+

_speed: speed_value _restriction_def
      | speed_value

_restriction_def: "(" _restrictions ")"

_restrictions: restriction
             | restriction "," _restrictions

restriction: weight                          -> weight_restriction
           | restriction_conditional         -> conditional_restriction
           | NUMBER+ "m"                     -> length_restriction
           | NUMBER+ "seats"                 -> seat_restriction
           | NUMBER+ "axles"                 -> axle_restriction

restriction_conditional: "articulated"  -> articulated
                       | "trailer"      -> trailer
                       | "caravan"      -> caravan
                       | "wet"          -> wet
                       | time_span      -> time

weight: WEIGHT "t"                   -> normal_weight
      | weight_qualifier WEIGHT "t"  -> qualified_weight_pre
      | WEIGHT "t" weight_qualifier  -> qualified_weight_post

weight_qualifier: "empty"     -> empty
                | "capacity"  -> capacity
                | "trailer"   -> trailer

interval: TIME "-" TIME  -> neg_interval
        | TIME "+" TIME  -> pos_interval

time_span: interval                               -> interval
         | "(" interval ")" "-" "(" interval ")"  -> complex_time_span

speed_value: NUMBER+ "mph" -> mph_speed
           | NUMBER+       -> kph_speed
           | "walk"        -> walk_speed

WEIGHT: /\d+([.]\d+)?/

TIME: /[a-zA-Z0-9:]+/

%import common.INT -> NUMBER
%import common.WS
%ignore WS
